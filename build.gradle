import org.gradle.api.JavaVersion

plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
}

def docx4jVersion = '11.4.7'
def jaxbVersion = '4.0.0'
def lombokVersion = '1.18.24'

group = 'uk.gov.digital.ho.hocs'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude(module: 'spring-boot-starter-tomcat')
	}
	implementation 'org.springframework.boot:spring-boot-starter-undertow'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

	implementation "org.docx4j:docx4j-core:${docx4jVersion}"
	implementation "org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4jVersion}"
	implementation "jakarta.xml.bind:jakarta.xml.bind-api:${jaxbVersion}"
	implementation "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
	implementation 'jakarta.activation:jakarta.activation-api:2.1.0'

	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor 'org.springframework:spring-context-indexer:5.3.21'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'

}

jar {
	enabled = false
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // report is always generated after tests run

}

plugins.withType(JacocoPlugin) {
	tasks["test"].finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	reports {
		xml.required = true
		html.required = false
		csv.required = false
		//for XML
		xml.destination file("${buildDir}/reports/jacoco.xml")
	}
}
