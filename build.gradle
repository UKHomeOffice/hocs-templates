plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

def docx4jVersion = '11.3.2'
def lombokVersion = '1.18.22'

group = 'uk.gov.digital.ho.hocs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

plugins.withType(JacocoPlugin) {
	tasks["test"].finalizedBy 'jacocoTestReport'
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
	reports {
		xml.required = true
		html.required = false
		csv.required = false
		//for XML
		xml.destination file("${buildDir}/reports/jacoco.xml")
	}
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude(module: 'spring-boot-starter-tomcat')
	}
	implementation 'org.springframework.boot:spring-boot-starter-undertow'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'net.logstash.logback:logstash-logback-encoder:7.0.1'

	implementation "org.docx4j:docx4j-core:${docx4jVersion}"
	implementation "org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4jVersion}"

	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor 'org.springframework:spring-context-indexer:5.3.14'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'

}

jar {
	enabled = false
}

test {
	useJUnitPlatform()
}
